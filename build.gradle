plugins {
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.1.0'
  id 'org.beryx.jlink' version '3.2.0'
}

repositories { mavenCentral() }

java { toolchain { languageVersion = JavaLanguageVersion.of(22) } }

javafx {
  version = '22.0.2'
  modules = [ 'javafx.controls' ]
}

application {
  mainClass = 'app.RdpLauncherApp'
  mainModule = 'app'
}

jlink {
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

  launcher {
    name = 'rdp-launcher' // 実行ファイル名
  }

  jpackage {
    // jpackageHome = 'C:/Program Files/Java/jdk-22'
    jpackageHome = System.getenv("JAVA_HOME")
    installerType = (findProperty('installerType') ?: 'msi') 
    appVersion = '0.1.1'  // バージョン情報

    // Windows 向け設定
    installerName = 'Windows Remote Desktop Launcher'
    imageName     = 'Windows Remote Desktop Launcher'

    icon = file('src/main/resources/app.ico').absolutePath

    imageOptions = []

    installerOptions += [
      '--win-menu',
      '--win-shortcut',
      '--win-per-user-install',

      // ★EULA表示（インストーラの「同意」画面）
      '--license-file', file('installer/EULA-ja.rtf').absolutePath,

      // インストール先変更を許可
      '--win-dir-chooser',

      // 固定GUIDを指定（アップグレード時に必要）
      '--win-upgrade-uuid', '6d9a1e04-27a0-4f98-982c-4845567754d8'
    ]
  }
}
